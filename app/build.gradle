/*
 * Copyright (C) 2017-2018 Hazuki
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

def gitHash = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm:ssXXX")

android {
    compileSdkVersion Integer.parseInt(project.android_compile_sdk_version)
    buildToolsVersion project.android_build_tools_version
    defaultConfig {
        applicationId "jp.hazuki.yuzubrowser"
        minSdkVersion Integer.parseInt(project.android_min_sdk_version)
        targetSdkVersion Integer.parseInt(project.android_target_sdk_version)
        versionCode Integer.parseInt(project.VERSION_CODE)
        versionName project.VERSION_NAME
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "GIT_HASH", "\"${gitHash}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        resValue "string", "package_name", applicationId
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            lintOptions {
                disable 'MissingTranslation', 'GoogleAppIndexingWarning', 'ClickableViewAccessibility'
            }
        }
        debug {
            ext.enableCrashlytics = false
            ext.alwaysUpdateBuildId = false
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/CHANGES'
        exclude 'META-INF/README.md'
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name == "release") {
            variant.outputs.each { output ->
                if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                    // Rename APK
                    def applicationId = defaultConfig.applicationId
                    def versionCode = defaultConfig.versionCode
                    def versionName = defaultConfig.versionName
                    def newName = "${applicationId}_v${versionName}_${versionCode}.apk"
                    if (output.hasProperty('outputFileName')) {
                        output.outputFileName = newName
                    } else {
                        output.outputFile = new File(output.outputFile.parent, newName)
                    }
                }
            }
        }
    }
}

androidExtensions {
    experimental = true
}

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://jitpack.io' }
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation "com.android.support:appcompat-v7:${android_support_lib_version}"
    implementation "com.android.support:design:${android_support_lib_version}"
    implementation "com.android.support:recyclerview-v7:${android_support_lib_version}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation "com.takisoft.fix:preference-v7:${android_support_fix_lib_version}"
    testImplementation 'junit:junit:4.12'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.5'
    implementation 'ca.barrenechea.header-decor:header-decor:0.2.8'
    implementation 'org.jsoup:jsoup:1.11.3'
    implementation project(':libraries:floatingactionbutton')
    implementation project(':libraries:colorpicker')
    implementation ('com.crashlytics.sdk.android:crashlytics:2.9.2@aar') {
        transitive = true
    }
    implementation 'com.github.turing-tech:MaterialScrollBar:13.2.3'
    implementation project(':libraries:asyncpermissions')
    implementation project(':libraries:BreadcrumbsView')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "org.jetbrains.anko:anko-commons:$anko_version"
    implementation "org.jetbrains.anko:anko-sqlite:$anko_version"
}
